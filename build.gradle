group = 'ru.vitkud.test'
version = '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all,-options,-rawtypes', '-Werror']
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

defaultTasks 'eclipse'

ext.buildDate = new Date()
ext.buildNumber = System.env['BUILD_NUMBER']?.toLong() ?: (long) buildDate.time.intdiv(1000).intdiv(60)

sourceSets {
	main {
		[java,resources]*.srcDirs = ['src']
	}
	test {
		[java,resources]*.srcDirs = ['test']
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile 'junit:junit:4.12'
	compileOnly 'org.eclipse.swt:org.eclipse.swt.win32.win32.x86:4.3'
}

tasks.eclipse.dependsOn(cleanEclipse)
eclipse {
	classpath {
		downloadJavadoc = true
	}
}
eclipseJdt << {
	file('.settings/org.eclipse.core.resources.prefs').write(
		'eclipse.preferences.version=1\n' +
		'encoding/<project>=UTF-8\n'
	)
}

jar {
	manifest {
		attributes (
			'Specification-Title': project.name,
			'Specification-Version': (version =~ /^(.*?)(?:-SNAPSHOT)?$/)[0][1],
			'Implementation-Title': project.name,
			'Implementation-Version': (version =~ /^(.*?)(?:-SNAPSHOT)?$/)[0][1] +
				".$buildNumber (${buildDate.format('yyyy-MM-dd')})",
		)
	}
}
